# bnf

<token> ::=
	<identifier>
	| <literal>
	| <operator>
	| <whitespace>
	| <comment>

<comment> ::=
	<hash> [ <anything>... ] <__lf__>

<anything> ::=
	<letter or number>
	| <less than operator>
	| <greater than operator>
	| <symbol>
	| <op char>
	| <hash>
	| <at sign>
	| <__space__>
	| <__tab__>
	| <__cr__>

<whitespace> ::=
	<whitespace char> <optional whitespace>

<optional whitespace> ::=
	[ <whitespace char>... ]

<whitespace char> ::=
	<__space__> | <__tab__> | <__cr__> | <__lf__>

<space> ::=
	<__space__>

<@identifier{str}> ::=
	<less than operator>
	[ <at sign> ]
	<identifier start>
	[ <identifier extension>... ]
	<greater than operator>

<identifier start> ::=
	<simple Latin letter>
	| <connector>

<identifier extension> ::=
	<letter or number>
	| <connector>
	| <space>
	| <left brace>
	| <right brace>

<@operator> ::=
	<production operator>
	| <ellipsis>
	| <op char>

# don't get confused with an actual <production operator>
<@production operator> ::=
	: : =

# Instead of ... to not get confused with an actual <ellipsis>
<@ellipsis> ::=
	. . .

<@literal{str}> ::=
	<non whitespace> [ <non whitespace>... ]

<letter or number> ::=
	<simple Latin upper case letter>
	| <simple Latin lower case letter>
	| <digit>

<non whitespace> ::=
	<letter or number>
	| <at sign>
	| <symbol>

<simple Latin letter> ::=
	<simple Latin upper case letter>
	| <simple Latin lower case letter>

<simple Latin upper case letter> ::=
	A | B | C | D | E | F | G | H | I | J | K | L | M | N | O
	| P | Q | R | S | T | U | V | W | X | Y | Z

<simple Latin lower case letter> ::=
	a | b | c | d | e | f | g | h | i | j | k | l | m | n | o
	| p | q | r | s | t | u | v | w | x | y | z

<digit> ::=
	0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9

<connector> ::=
	<underscore>

<symbol> ::=
	<double quote>
	| <quote>
	| <double quote>
	| <percent>
	| <ampersand>
	| <asterisk>
	| <plus sign>
	| <comma>
	| <minus sign>
	| <solidus>
	| <reverse solidus>
	| <colon>
	| <semicolon>
	| <equals operator>
	| <question mark>
	| <circumflex>
	| <underscore>
	| <backtick>
	| <tilde>
	| <exclamation point>

<op char> ::=
	<left paren>
	| <right paren>
	| <left bracket>
	| <right bracket>
	| <left brace>
	| <right brace>
	| <vertical bar>
	| <period>

<double quote> ::=
	"

<percent> ::=
	%

<ampersand> ::=
	&

<quote> ::=
	'

<left paren> ::=
	(

<right paren> ::=
	)

<asterisk> ::=
	*

<plus sign> ::=
	+

<comma> ::=
	,

<minus sign> ::=
	-

<period> ::=
	.

<solidus> ::=
	/

<reverse solidus> ::=
	\

<colon> ::=
	:

<semicolon> ::=
	;

<less than operator> ::=
	<__lt__>

<equals operator> ::=
	=

<greater than operator> ::=
	<__gt__>

<question mark> ::=
	?

<left bracket> ::=
	<__lsq__>

<right bracket> ::=
	<__rsq__>

<circumflex> ::=
	^

<underscore> ::=
	_

<vertical bar> ::=
	|

<left brace> ::=
	<__lbr__>

<right brace> ::=
	<__rbr__>

<at sign> ::=
	@

<hash> ::=
	<__hash__>

<backtick> ::=
	`

<tilde> ::=
	~

<exclamation point> ::=
	!
